buildscript {
    ext {
        flywaydbVersion = "7.6.0"
        jacksonCoreVersion = "2.12.7.1"
        jacksonDatatypeVersion = "2.12.1"
        lombokVersion = "1.18.26"
        postgresqlVersion = "42.2.27"
        spockVersion = "2.2-groovy-3.0"
        springBootVersion = "2.7.7"
        springDependencyManagmentVersion = "1.1.2"
        springFoxStarterVersion = "3.0.0"
        springFoxUiVersion = "3.0.0"
    }
}

plugins {
    id 'application'
    id 'checkstyle'
    id 'groovy'
    id 'io.spring.dependency-management' version "${springDependencyManagmentVersion}"
    id 'jacoco'
    id 'org.springframework.boot' version "${springBootVersion}"
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation "org.flywaydb:flyway-core:${flywaydbVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonCoreVersion}"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jacksonDatatypeVersion}"
    implementation "io.springfox:springfox-boot-starter:${springFoxStarterVersion}"
    implementation "io.springfox:springfox-swagger-ui:${springFoxUiVersion}"

    runtimeOnly "org.postgresql:postgresql:${postgresqlVersion}"
    
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.spockframework:spock-core:${spockVersion}"
    testImplementation "org.spockframework:spock-spring:${spockVersion}"
    testImplementation 'com.h2database:h2:2.2.220'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

application {
    mainClass = 'pl.futurecollars.invoicing.App'
}

tasks.named('test') {
    useJUnitPlatform()
}

checkstyle {
    showViolations = true
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
}

test {
    finalizedBy jacocoTestReport
    finalizedBy jacocoTestCoverageVerification
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                counter = 'BRANCH'
                minimum = 0.8
            }
        }
        rule {
            limit {
                counter = 'LINE'
                minimum = 0.8
            }
        }
    }
}
